buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.netflix.nebula:gradle-ospackage-plugin:3.4.0"
    }
}

apply plugin: 'java'
apply plugin:'application'
apply plugin: "nebula.ospackage"

version = '1.0.0.0'
mainClassName = "com.occi.leader.election.Main"
applicationName = 'occimon'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.apache.curator:curator-recipes:2.7.0'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    manifest {
        attributes 'Main-Class': 'com.occi.leader.election.Main'
    }
}

ospackage {
    os = LINUX
    arch = X86_64
    permissionGroup = 'root'
    user = 'root'
    fileMode = 0755
    license = 'Copyright AsiaInfo, Inc. 2017'
    vendor = 'AsiaInfo, Inc.'
    url = 'http://www.asiainfo.com'
    summary = 'OCCI logstash HA installation package. '

    from(jar.outputs.files) {
        into 'lib'
    }
    from(configurations.runtime) {
        into 'lib'
    }
    from("$buildDir/scripts") {
        into "bin"
    }
}

buildRpm {
    packageName = applicationName
    into '/opt/' + packageName + '-' + version
}
